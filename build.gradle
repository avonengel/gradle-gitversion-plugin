plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7'
    id 'org.unbroken-dome.test-sets' version '1.4.2'
    id 'com.gradle.plugin-publish' version '0.9.7'
}


repositories {
    jcenter()
}


testSets {
    integrationTest { dirName = 'integration-test' }
}


dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:4.5.0.201609210915-r'
    implementation 'com.google.guava:guava:20.0'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.1'

    testImplementation 'junit:junit:4.12'
    testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }

    integrationTestRuntimeOnly files(pluginUnderTestMetadata.outputDirectory)
}


gradlePlugin {

    plugins {
        gitVersionPlugin {
            id = 'org.unbroken-dome.gitversion'
            implementationClass = 'org.unbrokendome.gradle.plugins.gitversion.GitVersionPlugin'
        }
    }

    testSourceSets project.sourceSets.integrationTest
}

apply from: 'publishing.gradle'
